buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://dl.bintray.com/kotlin/kotlinx/"
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-serialization:1.3.61"
    }
}

plugins {
    id 'org.jetbrains.kotlin.multiplatform' version '1.3.61'
    id 'com.jfrog.bintray' version '1.8.4'
}

apply plugin: 'maven-publish'

group 'com.github.onotoliy.geojson'
version '1.1'

repositories {
    mavenCentral()
    maven {
        url "https://dl.bintray.com/kotlin/kotlinx/"
    }
    maven {
        url  "https://dl.bintray.com/a-k-pohresniy/onotoliy"
    }
}

kotlin {
    jvm(){
        mavenPublication {
            version = "$version"
            artifactId = "geojson-jvm"
        }
    }
    js(){
        compilations.all {
            kotlinOptions.moduleKind = "umd"
        }
        mavenPublication {
            version = "$version"
            artifactId = "geojson-js"
        }
    }
    metadata() {
        mavenPublication {
            version = "$version"
            artifactId = "geojson-metadata"
        }
    }
    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-common:0.14.0"
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }
        jsMain {
            dependsOn commonMain
            dependencies {
                implementation kotlin('stdlib-js')
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-js:0.14.0"
            }
        }
        jvmMain {
            dependsOn commonMain
            dependencies {
                implementation kotlin('stdlib-jdk8')
                implementation 'org.jetbrains.kotlinx:kotlinx-serialization-runtime:0.14.0'
            }
        }
        jvmTest {
            dependsOn jvmMain
            dependencies {
                implementation kotlin('stdlib-jdk8')
                implementation 'org.jetbrains.kotlinx:kotlinx-serialization-runtime:0.14.0'
                implementation 'junit:junit:4.13'
            }
        }
    }
}

configurations {
    compileClasspath
}

publishing {
    repositories {
        maven{
            url = 'https://dl.bintray.com/a-k-pohresniy/onotoliy'
        }
    }

    publications {
        jvm() {
            artifactId = "geojson-jvm"
        }
        js() {
            artifactId = "geojson-js"
        }
        metadata() {
            artifactId = "geojson-metadata"
        }
    }
}

bintray {
    user = property('onotoliy.bintray.user')
    key = property('onotoliy.bintray.key')
    publications = ['jvm', 'js', 'metadata']
    publish = true
    pkg {
        repo = 'onotoliy'
        name = 'geojson'
        userOrg = 'a-k-pohresniy'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/onotoliy/geojson.git'
    }
    version {
        name = project.version
        released = new Date()
        vcsTag = project.version

        gpg {
            sign = true
            passphrase = property('onotoliy.bintray.passphrase')
        }
    }
}